# Conditional Ternary Operator

```verilog
(condition ? if_true : if_false)
```

This can be used to choose one of two values based on condition (a mux!) on one line, without using an if-then inside a combinational always block.

Examples:

```verilog
(0 ? 3 : 5)     // This is 5 because the condition is false.
(sel ? b : a)   // A 2-to-1 multiplexer between a and b selected by sel.

always @(posedge clk)         // A T-flip-flop.
  q <= toggle ? ~q : q;

always @(*)                   // State transition logic for a one-input FSM
  case (state)
    A: next = w ? B : A;
    B: next = w ? A : B;
  endcase

assign out = ena ? q : 1'bz;  // A tri-state buffer

((sel[1:0] == 2'h0) ? a :     // A 3-to-1 mux
 (sel[1:0] == 2'h1) ? b :
                      c )
```

## Practice

Given four unsigned numbers, find the minimum. Unsigned numbers can be compared with standard comparison operators (a < b). Use the conditional operator to make two-way min circuits, then compose a few of them to create a 4-way min circuit. You'll probably want some wire vectors for the intermediate results.

### Solutions:

```verilog
module top_module (
    input [7:0] a, b, c, d,
    output [7:0] min);//

    // assign intermediate_result1 = compare? true: false;
    wire [7:0]tmp;
    wire [7:0]tmp1;
    wire [7:0]tmp2;
    assign tmp = (a < b) ? a : b;
    assign tmp1 = (tmp < c) ? tmp : c;
    assign tmp2 = (tmp1 < d)? tmp1 : d;
    assign min = tmp2;

endmodule
```

# Reduction Operator

You're already familiar with bitwise operations between two values, e.g., a & b or a ^ b. Sometimes, you want to create a wide gate that operates on all of the bits of one vector, like (a[0] & a[1] & a[2] & a[3] ... ), which gets tedious if the vector is long.

The reduction operators can do AND, OR, and XOR of the bits of a vector, producing one bit of output:

```verilog
& a[3:0]     // AND: a[3]&a[2]&a[1]&a[0]. Equivalent to (a[3:0] == 4'hf)
| b[3:0]     // OR:  b[3]|b[2]|b[1]|b[0]. Equivalent to (b[3:0] != 4'h0)
^ c[2:0]     // XOR: c[2]^c[1]^c[0]
```

These are unary operators that have only one operand (similar to the NOT operators ! and ~). You can also invert the outputs of these to create NAND, NOR, and XNOR gates, e.g., (~& d[7:0]).

Now you can revisit 4-input gates and 100-input gates.

## Practice

Parity checking is often used as a simple method of detecting errors when transmitting data through an imperfect channel. Create a circuit that will compute a parity bit for a 8-bit byte (which will add a 9th bit to the byte). We will use "even" parity, where the parity bit is just the XOR of all 8 data bits.

```verilog
module top_module (
    input [7:0] in,
    output parity);
	assign parity = ^in;
endmodule

```

# Reduction: Even wider gates

Build a combinational circuit with 100 inputs, in[99:0].

There are 3 outputs:

- out_and: output of a 100-input AND gate.
- out_or: output of a 100-input OR gate.
- out_xor: output of a 100-input XOR gate.

## solutions:

```verilog
module top_module(
    input [99:0] in,
    output out_and,
    output out_or,
    output out_xor
);
    assign out_and = &in;
    assign out_or = | in;
    assign out_xor = ^in;

endmodule
```

# Combinational For-loop: Vector reversal 2

Given a 100-bit input vector [99:0], reverse its bit ordering.

## Solutions:

```verilog
module top_module(
    input [99:0] in,
    output [99:0] out
);

    integer i = 0;
    always @(*) begin
        i = 0;    // remember to init i here otherwise will creates latch
        while (i<=99) begin
            out[99-i] = in[i];
            i=i+1;
        end
    end

endmodule
```

## Sample solution using $bits()

```verilog
module top_module (
	input [99:0] in,
	output reg [99:0] out
);

	always @(*) begin
		for (int i=0;i<$bits(out);i++)		// $bits() is a system function that returns the width of a signal.
			out[i] = in[$bits(out)-i-1];	// $bits(out) is 100 because out is 100 bits wide.
	end

endmodule
```

# Combinational For-loop: 255-bit population count

A "population count" circuit counts the number of '1's in an input vector. Build a population count circuit for a 255-bit input vector.

## solution

```verilog
module top_module(
    input [254:0] in,
    output [7:0] out );

    integer count;
    integer i;
    always @(*) begin
        i = 0;
        count = 0;
        while (i < $bits(in)) begin
            if (in[i] == 1)
                count++;

            i++;
        end
        out = count;
    end

endmodule
```

## Sample solution without if block

```verilog
module top_module (
	input [254:0] in,
	output reg [7:0] out
);

	always @(*) begin	// Combinational always block
		out = 0;
		for (int i=0;i<255;i++)
			out = out + in[i];
	end

endmodule

```
